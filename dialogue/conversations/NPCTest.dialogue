#using GameDialogueManager

~ start
if GlobalVariables.finish_mini_game:
	=> finished_mini_game
Byte: Hi there!
- Where… am I? Who are… you? => game_lore
- What do you want me to do? => find_the_scanner
- Nevermind. => END!

~ game_lore
Byte: I’m Byte the firefly! And this is my home. Welcome to Evergrove!
- Byte?
	Byte: That’s me! But don’t worry! Fireflies don’t bite! Haha! What’s your name?
	- My name is… um…
		Byte: Oh! Don’t worry! I forget things all the time too! 
		Byte: I'll call you... {{GlobalVariables.username}}
		Byte: {{GlobalVariables.username}}! Now, that is a great name!!
		- Thank you.
			Byte:  I’m so glad you came to Evergrove, {{GlobalVariables.username}}! We really need your help!
			- My help?
				Byte: Yes! Professor Byte has gone missing, and Evergrove is in trouble! You have to help us!
				- Professor Byte?
					Byte: Yes! Professor Byte was the guardian of the Great Data Archives! 
					Byte: The archive contained data on every bug, beetle, and butterfly in Evergrove! 
					Byte: It was a powerful database that kept track of every species' traits, habits, and secrets.
					- Secrets?
						Byte: So many secrets! But after the great disaster struck, the archives scattered data across the land. 
						Byte:  I call it The Wipeout. Now, the bugs roam freely, but their information is lost! All gone!
						-  Is that bad?
							Byte: [shake rate=10.0 level=6 connected=0]IT'S HORRIBLE![/shake]
							Byte: Without this knowledge, researchers and scientists aren't able to understand or protect the creatures of Evergrove!
							 - How can I help?
								Byte: You can be a Data Explorer! I can help you learn how! Together, we can restore The Great Archive!
								- That sounds like fun! How do we get started?
									Byte: Professor Byte equipped me with access to the archive so I’m sort of… connected to the database?
									Byte: Even so, my processor was scrambled after The Wipeout.
									Byte: I can share what’s left in my memory of the archive with the BugScanner 3000.

~ find_the_scanner
if !GlobalVariables.has_scanner
	Byte: Go get the BugScanner 3000! It’s on top of a log nearby.
while !GlobalVariables.has_scanner
	% Byte: The BugScanner 3000 is close by. It's on top of a log.
	% Byte: The BugScanner 3000 is near the southern path.
	% Byte: Quickly! Find the BugScanner 3000!
	% Byte: The BugScanner 3000 is close by, but I can't pick it up.
	=> find_the_scanner

# Dialogue pauses until condition is met
if GlobalVariables.has_scanner
	Byte: Yippee! You found it!
	do SignalBus._show_byte()
	do SignalBus._hide_npc_byte()
	=> find_the_bugs
	
~ find_the_bugs
# Addition from 4/10
# click is enabled when the tutorial begins
if !GlobalVariables.minigame_ready
	Byte: Now, let’s go find some bugs and take a look at their data!
while !GlobalVariables.minigame_ready
	% Byte: The other bugs won't be far from where we met!
	% Byte: I hope we find the other bugs soon...
	% Byte: I can't wait for you to meet the other creatures in Evergrove!
	% Byte: The BugScanner 3000 should pick up on other bugs any minute!
	=> find_the_bugs
if GlobalVariables.minigame_ready
	=> found_the_bugs

~ found_the_bugs
while GlobalVariables.minigame_ready
	Byte: The BugScanner 3000 must have seen the bugs!
	Byte: Press 't' to use the BugScanner 3000!
	=> found_the_bugs

#~ mini_game
# drag is enabled when the tutorial_state_machine finishes
#if !EnableVariables.dragging_enabled
#	Byte: I’ll walk you through how the archive is organized so you can fill in the missing data on these bugs!
#	Byte: In the archive, each row represents a record of a bug living in Evergrove!
#	Byte: And look! The record for ALBY is already complete! Click on Alby to take a closer look!
#	Byte: The first column variable is NAME. Notice how each bug’s name is listed in this column?
#	Byte: The next variable is TOTAL LEGS. If you count ALBY’s legs, there should be 6 in total!
#	Byte: The last variable is COLOR. ALBY is mostly YELLOW, so the value in her record is also YELLOW!
#	Byte: That’s all you need to know to get started!
#	Byte: Click on the variable NAME of each bug to examine them and complete the data!
#	# finish_mini_game is set to true after the last correst datablock is placed into the last open slot
#	if GlobalVariables.finish_mini_game
#		Byte: You completed the table! Great job!
#		=> END!

~ finished_mini_game
if GlobalVariables.finish_mini_game
	Byte: [wave rate=10.0 level=10 connected=0][color=#6A1B9A]You did it!!! Great Job!!![/color][/wave]
	Byte: Oh {{GlobalVariables.username}}... I feel... STRANGE...
	Byte: [shake rate=7.0 level=6 connected=1]SOMETHING IS HAPPENING TO ME!!![/shake]
	Byte: Oh my! My processing power just spiked— [b]there's so much more data in the archive now![/b]
	- Really? What does that mean?
		Byte: It means that all the data we need is back! We just have to collect it by tracking down the bugs! 
		Byte: Now, whenever we find new groups of bugs, I can show you how to work with their data!
		Byte: Together, we'll learn to protect and understand the creatures of Evergrove— just like real scientists do!
		- Can I become a real scientist?
			Byte: You’re still a Data Explorer for now! 
			Byte: But with more experience, you can become a scientist in no time! 
			Byte: Come on! There’s no time to waste! Let’s go look for some more bugs!
			do SignalBus._show_byte()
			do SignalBus._hide_npc_byte()
			=> END!

=> END